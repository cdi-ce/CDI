Actor Marauder
{
Monster
Health 10000
Radius 20
Height 56
Mass 1000
Speed 14
+BOSS
+MISSILEMORE
+MISSILEEVENMORE
+DONTRIP
+NOTIMEFREEZE
+FLOORCLIP
+QUICKTORETALIATE
+NORADIUSDMG
+DONTHARMSPECIES
+DROPOFF
PainChance 12
MaxStepHeight 32
MaxDropOffHeight 32
SeeSound "Marauder/See"
PainSound "Marauder/Pain"
DeathSound "Marauder/Death"
ActiveSound "Marauder/Idle"
Species "Hell"
DamageFactor "Hell", 0.0
DamageFactor "Legendary", 0.5
DamageFactor "BFGSplash", 0.25
DamageFactor "PDTBFG", 0.2
DamageFactor "PDTBFGTracer", 0.2
DamageFactor "MechStep", 0.0
DamageFactor "Pierce", 0.25
DamageFactor "MarinePierce", 0.25
DamageFactor "Kaboom", 0.2
DamageFactor "MarineKaboom", 0.2
DamageFactor "Legendary", 0.2
DamageFactor "BFGSplash", 0.25
DamageFactor "PDTBFG", 0.05
DamageFactor "PDTBFGTracer", 0.05
DamageFactor "PlayerDevBall", 0.15
DamageFactor "PlayerDevTracer", 0.15
DamageFactor 0.45
DropItem "BonusCrateDrop" 1
Obituary "The Marauder prooved that %o was nothing but a usurper."
Tag "\c[W8]Marauder\c-"
var int user_script;
var int user_delay;
var int user_missile;
States
{
  Spawn:
    TNT1 A 0 A_JumpIf(user_script == 1,3)
	TNT1 A 0 ACS_NamedExecuteAlways("Strong Monster Phase Setter",0,0,0,0)
    TNT1 A 0 A_SetUserVar("user_script",1)
	MARD A 3 A_Look
	Loop
  See:
    TNT1 A 0 A_JumpIf(user_script == 1,3)
	TNT1 A 0 ACS_NamedExecuteAlways("Strong Monster Phase Setter",0,0,0,0)
    TNT1 A 0 A_SetUserVar("user_script",1)
	TNT1 A 0 A_UnSetInvulnerable
	TNT1 A 0 A_JumpIfTargetInLOS("Strafing")
    MARD AABB 1 A_Chase
    TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
    MARD CCDD 1 A_Chase
    TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Jump(48,"SeeShield")
	Loop
  SeeShield:
	TNT1 A 0 A_SetInvulnerable
	TNT1 A 0 A_JumpIfTargetInLOS("StrafingShield")
    MARS AABB 1 A_Chase
    TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
    MARS CCDD 1 A_Chase
    TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Jump(48,"See")
	Loop
  Strafing:
	TNT1 A 0 A_UnSetInvulnerable
	TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Chase("","Missile",CHF_DONTMOVE)
	MARD AABB 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Chase("","Missile",CHF_DONTMOVE)
	MARD CCDD 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_Jump(48,"SeeShield")
	Goto See
  StrafingShield:
	TNT1 A 0 A_SetInvulnerable
	TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Chase("","Missile",CHF_DONTMOVE)
	MARS AABB 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_SpawnItem("FootStep",0,0,0,0)
	TNT1 A 0 A_Chase("","Missile",CHF_DONTMOVE)
	MARS CCDD 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_Jump(48,"See")
	Goto SeeShield
  Missile:
	TNT1 A 0 A_Jump(64,2)
	TNT1 A 0 A_JumpIfCloser(300,"ExplosiveSSG")
	TNT1 A 0 A_Jump(32,"Apparition")
	TNT1 A 0 A_Jump(100,"ExplosiveSSG","ChargedAttack")
  Normal:
	TNT1 A 0
	TNT1 A 0 A_UnSetInvulnerable
	TNT1 A 0 A_SetUserVar("user_missile",0)
  NormalGo:
	MARD AAUU 3 A_GiveInventory("SpecOpsStrafing",1)
	MARD W 3 Bright A_GiveInventory("MarauderAttack",1)
	TNT1 A 0 A_CPosRefire
	TNT1 A 0 A_SetUserVar("user_missile",user_missile+1)
	TNT1 A 0 A_JumpIf(user_missile >= 6,"NormalEnd")
	TNT1 A 0 A_JumpIf(user_missile >= 3,1)
	Loop
	TNT1 A 0 A_Jump(128,"ChargedAttack","ExplosiveSSG","Dodge","Dodge")
	Loop
  NormalEnd:
	TNT1 A 0 A_Jump(224,"ChargedAttack","ExplosiveSSG","Dodge","Dodge")
	Goto See
  ExplosiveSSG:
	TNT1 A 0 A_JumpIfCloser(550,1)
	Goto Missile
	MARD AAEEE 3 A_GiveInventory("SpecOpsStrafing",1)
    TNT1 A 0 A_PlayWeaponSound("ExpSSG/Fire")
    TNT1 AAAAAAAA 0 A_CustomMissile("HitlerExplosiveTracer",random(36,39),0,random(8,-8),0)
    TNT1 A 0 A_GiveInventory("SpecOpsStrafing",1)
	MARD F 5 Bright A_CustomBulletAttack(13.8,9.6,10,random(2,4)*6,"HellBulletPuff")
	MARD EEEE 3 A_GiveInventory("SpecOpsStrafing",1)
    TNT1 AA 0 A_SpawnItemEx("OrangeShellCasing",12,6,30,random(2,6),0,random(1,3),random(-80,-90),128)
	MARD EEEE 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_CPosRefire
	TNT1 A 0 A_Jump(144,"ExplosiveSSG","Missile","Dodge","Dodge")
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  ChargedAttack:
	MARD UU 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_PlayWeaponSound("Marauder/Eyes")
	TNT1 A 0 A_SetUserVar("user_missile",0)
	MARD ZZZZZZ 3 Bright A_GiveInventory("MarauderEyes",1)
	TNT1 A 0 A_JumpIfCloser(700,1)
	Goto ChargedAttackGo
  ChargedAttackFly:
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_Stop
	TNT1 A 0 ThrustThingZ(0,40,0,1)
	TNT1 A 0 A_PlayWeaponSound("Marauder/Attack")
	MARD WWWWWWWWWWWWWWWWWWWW 1 Bright A_GiveInventory("MarauderJump",1)
  ChargedAttackFlyCheck:
	TNT1 A 0 A_JumpIf(user_delay >= 105,"ChargedAttackLand")
	TNT1 A 0 A_SetUserVar("user_delay",user_delay+1)
	TNT1 A 0 A_Explode(3,256)
	TNT1 A 0 A_SpawnItemEx("MarauderGhost")
	MARD Z 1 Bright A_CheckFloor("ChargedAttackLand")
	Loop
  ChargedAttackLand:
	TNT1 A 0 A_CustomMissile("MarauderBomb",32,0,0,CMF_AIMDIRECTION)
	TNT1 A 0 Radius_Quake(175,8,512,1024,0)
	MARD Z 1 Bright A_SpawnItemEx("GreenBossExplosionEffect")
	TNT1 A 0 A_SetUserVar("user_missile",user_missile+1)
	TNT1 A 0 A_JumpIf(user_missile >= 3,2)
	TNT1 A 0 A_Jump(155,"ChargedAttackFly")
	TNT1 A 0 A_Jump(192,"Normal","Dodge","Dodge")
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  ChargedAttackGo:
	MARD ZZZZZZ 3 Bright A_GiveInventory("MarauderEyes",1)
	TNT1 A 0 A_CustomMissile("DEradicationBFGProjectile",0,32,0)
	MARD WWWWWWWWWWWWWWWW 3 Bright A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_CPosRefire
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  Apparition:
	TNT1 A 0 A_Jump(128,"ApparitionSeeker")
	MARD AAAA 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_SetInvulnerable
	TNT1 A 0 A_PlayWeaponSound("Marauder/Dog")
	MARD UUUUUUUUUUUUUUUUUUUUUU 3 Bright A_GiveInventory("LegZombieStrafing",1)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,-100,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,100,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,-60,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,60,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,-20,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_CustomMissile("LegendaryApparition",0,20,0,CMF_AIMDIRECTION)
	TNT1 A 0 A_UnSetInvulnerable
	MARD WWWWW 3 Bright A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  ApparitionSeeker:
	MARD AAAA 3 A_GiveInventory("SpecOpsStrafing",1)
	MARD AAAA 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_SetInvulnerable
	TNT1 A 0 A_PlayWeaponSound("Marauder/Dog")
	MARD UUUUUUUUUUUUUUUUUUUUUU 3 Bright A_GiveInventory("LegZombieStrafing",1)
	TNT1 A 0 A_CustomMissile("LegendaryApparitionSeeker",0,0,0)
	TNT1 A 0 A_UnSetInvulnerable
	MARD WWWWW 3 Bright A_GiveInventory("SpecOpsStrafing",1)
	MARD UUUUU 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_CustomMissile("LegendaryApparitionSeeker",0,0,0)
	MARD WWWWW 3 Bright A_GiveInventory("SpecOpsStrafing",1)
	MARD UUUUU 3 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_CustomMissile("LegendaryApparitionSeeker",0,0,0)
	MARD WWWWW 3 Bright A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  Dodge:
	MARD A 15 A_GiveInventory("MarauderDodge",1)
	TNT1 A 0 A_Jump(192,"Normal","Dodge")
	TNT1 A 0 A_Jump(96,"SeeShield")
	Goto See
  Pain:
    MARD G 2
    MARD G 2 A_Pain
	TNT1 A 0 A_Jump(192,"Missile")
	TNT1 A 0 A_Jump(96,"SeeShield")
    Goto See
  Death:
	MARD G 11 A_ScreamAndUnblock
	TNT1 A 0 A_GiveToTarget("EpicCrateStrongGiver",1)
	TNT1 AAAAA 0 A_GiveToTarget("BossCrateGiver",1)
	MARD H 3 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	MARD HHHIII 4
	MARD J 3 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	MARD K 6 A_SpawnItem("BodyCrash",0,0,0,0)
	MARD LM 6
	MARD N -1
	Stop
}
}

Actor MarauderProj : EpicBladeProj
{
-MTHRUSPECIES
-FOILINVUL
DamageType "Hell"
Damage 5
States
{
  Death:
	TNT1 A 0 A_SetTranslucent(0.85,1)
	DKAT D 3 Bright
	TNT1 A 0 A_SpawnItemEx("RedParticleSpawner",0,0,0,0,0,0,0,128)
	TNT1 AAA 0 A_CustomMissile("Kaboom10",0,0,random(0,360),2,random(0,360))
	TNT1 AAA 0 A_CustomMissile("SmokeFX4",0,0,random(0,360),2,random(0,360))
	DKAT E 3 Bright A_Explode(8,64)
	DKAT FGIJKLM 3 Bright
	Stop
}
}

Actor MarauderAttack : CustomInventory
{
Inventory.PickupMessage ""
Inventory.PickupSound ""
-COUNTITEM
+INVENTORY.AUTOACTIVATE
States
{
  Pickup:
	TNT1 A 1 A_FaceTarget
	TNT1 A 1 A_PlayWeaponSound("Marauder/Attack")
	TNT1 A 1 A_GiveInventory("SpecOpsStrafing",1)
	TNT1 A 1 A_Jump(256,1,2,3,4)
	Stop
	TNT1 A 1 A_GiveInventory("MarauderAttack1",1)
	Stop
	TNT1 A 1 A_GiveInventory("MarauderAttack2",1)
	Stop
	TNT1 A 1 A_GiveInventory("MarauderAttack3",1)
	Stop
	TNT1 A 1 A_GiveInventory("MarauderAttack4",1)
	Stop
}
}

Actor MarauderAttack1 : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_CustomMissile("MarauderProj",32,20,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,-20,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,-10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,0,0,CMF_AIMOFFSET)
	Stop
}
}

Actor MarauderAttack2 : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_CustomMissile("MarauderProj",52,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",42,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",22,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",12,0,0,CMF_AIMOFFSET)
	Stop
}
}

Actor MarauderAttack3 : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_CustomMissile("MarauderProj",52,20,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",42,10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",22,-10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",12,-20,0,CMF_AIMOFFSET)
	Stop
}
}

Actor MarauderAttack4 : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_CustomMissile("MarauderProj",52,-20,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",42,-10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",32,0,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",22,10,0,CMF_AIMOFFSET)
	TNT1 A 1 A_CustomMissile("MarauderProj",12,20,0,CMF_AIMOFFSET)
	Stop
}
}

Actor MarauderDodge : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_FaceTarget
	TNT1 A 1 A_Jump(128,2)
	TNT1 A 1 A_GiveInventory("AIJumping",1)
	TNT1 A 1 A_Jump(256,1,3,5,7)
	Stop
	TNT1 A 1 ThrustThing(angle*256/360+192,15,0,0)
	TNT1 A 1 A_Recoil(15)
	Stop
	TNT1 A 1 ThrustThing(angle*256/360+192,15,0,0)
	TNT1 A 1 A_Recoil(-15)
	Stop
	TNT1 A 1 ThrustThing(angle*256/360+64,15,0,0)
	TNT1 A 1 A_Recoil(15)
	Stop
	TNT1 A 1 ThrustThing(angle*256/360+64,15,0,0)
	TNT1 A 1 A_Recoil(-15)
	Stop
}
}

Actor MarauderEyes : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_SpawnItemEx("MarauderCirclePadEffect")
	TNT1 A 1 A_GiveInventory("SpecOpsStrafing",1)
	Stop
}
}

Actor MarauderJump : MarauderAttack
{
States
{
  Pickup:
	TNT1 A 1 A_Recoil(-2)
	TNT1 A 1 A_SpawnItemEx("MarauderGhost")
	Stop
}
}


Actor MarauderGhost
{
RenderStyle Translucent
Alpha 0.75
+NOGRAVITY
+CLIENTSIDEONLY
+DONTSPLASH
+NOINTERACTION
+NOCLIP
States
{
  Spawn:
	MARD Z 2 A_FadeOut(0.18)
    Loop
}
}

Actor MarauderBomb
{
Projectile
Speed 0
Damage 0
+NOCLIP
DamageType "Hell"
States
{
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Explode(192,512)
    Stop
}
}