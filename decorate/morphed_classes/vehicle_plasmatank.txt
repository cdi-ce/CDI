//========================================================= \\
// ---------------------- Plasma Tank --------------------- \\
//========================================================= \\

Actor PlasmaTank : MechPickup
{
  States
  {
  Spawn:
    MECH C 1 A_SpawnItemEx("MechIndicator",0,0,88)
    Loop
  Pickup:
  	NULL A 1
	NULL A 1 A_JumpIf(ACS_ExecuteWithResult(3703,0,0,0) == 1 ,"Nope")
	NULL A 1 ACS_ExecuteAlways(2994,0,0,0,0)
    NULL A 1 A_GiveInventory("PlasmaTankGiver")
	Stop
  Nope:
	MECH C 1
	Stop
  }
}

Actor PlasmaTank_Used1 : PlasmaTank
{
  States
  {
  Pickup:
  	NULL A 1
	NULL A 1 A_JumpIf(ACS_ExecuteWithResult(3703,0,0,0) == 1 ,"Nope")
	NULL A 1 ACS_ExecuteAlways(2994,0,0,0,0)
    NULL A 1 A_GiveInventory("PlasmaTankGiver_Used1")
	Stop
  }
}

Actor PlasmaTank_Used2 : PlasmaTank
{
  States
  {
  Pickup:
  	NULL A 1
	NULL A 1 A_JumpIf(ACS_ExecuteWithResult(3703,0,0,0) == 1 ,"Nope")
	NULL A 1 ACS_ExecuteAlways(2994,0,0,0,0)
    NULL A 1 A_GiveInventory("PlasmaTankGiver_Used2")
	Stop
  }
}

Actor PlasmaTank_Used3 : PlasmaTank
{
  States
  {
  Pickup:
  	NULL A 1
	NULL A 1 A_JumpIf(ACS_ExecuteWithResult(3703,0,0,0) == 1 ,"Nope")
	NULL A 1 ACS_ExecuteAlways(2994,0,0,0,0)
    NULL A 1 A_GiveInventory("PlasmaTankGiver_Used3")
	Stop
  }
}

ACTOR PlasmaTankGiver : MechGiver{Powerup.Type "PlasmaTank"}
ACTOR PlasmaTankGiver_Used1 : PlasmaTankGiver{Powerup.Type "PlasmaTank_Used1"}
ACTOR PlasmaTankGiver_Used2 : PlasmaTankGiver{Powerup.Type "PlasmaTank_Used2"}
ACTOR PlasmaTankGiver_Used3 : PlasmaTankGiver{Powerup.Type "PlasmaTank_Used3"}

ACTOR PowerPlasmaTank : PowerMorph
{
  PowerMorph.PlayerClass "Mech_PlasmaTank"
  PowerMorph.MorphFlash "Nothing"
  PowerMorph.UnMorphFlash "Nothing"
  PowerMorph.MorphStyle(MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE|MRF_LOSEACTUALWEAPON)
}

ACTOR PowerPlasmaTank_Used1 : PowerPlasmaTank{PowerMorph.PlayerClass "Mech_PlasmaTank_Used1"}
ACTOR PowerPlasmaTank_Used2 : PowerPlasmaTank{PowerMorph.PlayerClass "Mech_PlasmaTank_Used2"}
ACTOR PowerPlasmaTank_Used3 : PowerPlasmaTank{PowerMorph.PlayerClass "Mech_PlasmaTank_Used3"}

ACTOR Mech_PlasmaTank : BaseMech
{
  Health 700
  Player.DisplayName "Plasma Tank"
  Player.MaxHealth 700
  Player.MorphWeapon "PlasmaTank-Weapons"
  Player.ForwardMove 0.6
  Player.SideMove 0.25
  States
  {
  Spawn:
    MTPK A 1
    Loop
  See: 
    MTPK A 0 A_PlaySound("ZTank/Run")
    MTPK A 6
    Loop
  Missile:
    MTPK DE 3 Bright
    Goto Spawn	
  Death:
    MTPK G 4
    MTPK G 4 A_PlaySound("ZTank/death")
	MTPK HHHHHIIIIIKKKKKLLLLLMMMMMNNNNN 2 Bright A_SpawnItemEx("NewSmallKaboomEffect",random(-24,24),random(-24,24),random(8,48))
    TNT1 A 0 A_Fall
	TNT1 A 0 A_SpawnItemEx("SmallVehicleExplosionEffect") 
    MTPK O -1
    Stop
  }
}

ACTOR Mech_PlasmaTank_Used1 : Mech_PlasmaTank{Health 525}
ACTOR Mech_PlasmaTank_Used2 : Mech_PlasmaTank{Health 350}
ACTOR Mech_PlasmaTank_Used3 : Mech_PlasmaTank{Health 175}

ACTOR PlasmaTank-Weapons : MechWeapon
{
  Obituary "%o was destroyed by %k's plasma Tank."
  States
  {
  Fire:
    MHUD A 0 A_JumpIfInventory("MechPlasmaCasterReload",1,"AltFire")
    MHUD A 0 A_GiveInventory("MechPlasmaCasterReload",1)
    MHUD A 0 ACS_ExecuteAlways(3104,0,2)
	MHUD A 0 ACS_ExecuteAlways(3113,0,0)
	MHUD A 0 
    Goto Ready	
  AltFire:
	MHUD A 0 A_JumpIfInventory("MechRailgunReload",1,"Fire3")
    MHUD A 0 A_GiveInventory("MechRailgunReload",1)
    MHUD B 0 ACS_ExecuteAlways(3104,0,3)
	MHUD A 0 ACS_ExecuteAlways(3113,0,1)
	MHUD A 0 
    Goto Ready
  Fire3:	
	MHUD B 0 A_GunFlash
	MHUD B 0 A_PlayWeaponSound("plasmagun/fire")
	MHUD B 0 A_FireCustomMissile("PlasmaProj",0,0,-28,38)
    MHUD B 3 Bright A_FireCustomMissile("PlasmaProj",0,0,-28,28)
	MHUD A 2
	MHUD B 0 A_GunFlash
	MHUD B 0 A_PlayWeaponSound("plasmagun/fire")
	MHUD B 0 A_FireCustomMissile("PlasmaProj",0,0,28,38)
    MHUD B 3 Bright A_FireCustomMissile("PlasmaProj",0,0,28,28)	
    MHUD A 1 A_Refire
    Goto Ready
  }
}

ACTOR PlasmaTankFire-PlasmaCaster1 : CustomInventory
{       
Inventory.Amount 1
Inventory.MaxAmount 1
+INVENTORY.QUIET
+INVENTORY.UNDROPPABLE
+INVENTORY.ALWAYSPICKUP
+INVENTORY.AUTOACTIVATE
States
{
  Use:
	TNT1 A 0 
	TNT1 A 0 A_PlaySoundEx("plasmacaster/fire",0,FALSE)
	TNT1 A 0 A_FireCustomMissile("PlasmaCasterProj",0,0,28,28)
	Stop
}
}

ACTOR PlasmaTankFire-PlasmaCaster2 : PlasmaTankFire-PlasmaCaster1
{
States
{
  Use:
	TNT1 A 0 
	TNT1 A 0 A_PlaySoundEx("plasmacaster/fire",0,FALSE)
	TNT1 A 0 A_FireCustomMissile("PlasmaCasterProj",0,0,-28,28)
	Stop
}
}

ACTOR PlasmaTankFire-RailgunLoad : PlasmaTankFire-PlasmaCaster1
{
States
{
  Use:
	TNT1 A 0 
	TNT1 A 0 A_PlaySoundEx("weapons/rgcharge",0,FALSE)
	Stop
}
}

ACTOR PlasmaTankFire-Railgun1 : PlasmaTankFire-PlasmaCaster1
{
States
{
  Use:
	TNT1 A 0 
	TNT1 A 0 A_PlaySoundEx("monsters/darkcyberrail",0,FALSE)
	TNT1 A 0 A_FireCustomMissile("RailgunProj",0,0,-26,22)
	Stop
}
}

ACTOR PlasmaTankFire-Railgun2 : PlasmaTankFire-PlasmaCaster1
{
States
{
  Use:
	TNT1 A 0 
	TNT1 A 0 A_PlaySoundEx("monsters/darkcyberrail",0,FALSE)
	TNT1 A 0 A_FireCustomMissile("RailgunProj",0,0,26,22)
	Stop
}
}

ACTOR PlasmaProj
{
  Speed 70
  Radius 6
  Height 6
  Damage 9
  Projectile
  Renderstyle Add
  Alpha 0.75
  Scale 0.22
  +RANDOMIZE
  +MTHRUSPECIES
  //+NOTIMEFREEZE
  DeathSound "plasma/exp"
  Decal HeatScorch
  States
  {
  Spawn:
    PLST ABCDEF 3 Bright A_SpawnItemEx("PlasmaProjTrail",0,0,0,0,0,0,0,128)
    Loop
  Death:
	TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner",0,0,0,0,0,0,0,128)
	TNT1 AAA 0 A_CustomMissile("Kaboom6", 0, 0, random(0,360), 2, random(0,360))
	TNT1 AAA 0 A_CustomMissile("SmokeFX4", 0, 0, random(0,360), 2, random(0,360))
    TNT1 A 1 A_SpawnItem("PlasmaEx")   
    Stop
  }
}

ACTOR PlasmaProjTrail : ClientSideEffect
{
Alpha 0.6
Scale 0.176
RenderStyle Add
PROJECTILE
+CLIENTSIDEONLY
+NOBLOCKMAP
+BRIGHT
+DONTSPLASH
States
{
Spawn:
	TNT1 A 0
	TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(304) == 1, "Toaster")
	TNT1 A 1
    PLST ABCDEF 1 Bright A_FadeOut(0.05)
	Goto Spawn+1
    }
}

Actor PlasmaCasterProj
{
    Radius 12
    Height 12
    Speed 80
    Damage 10
    Scale 2
    PROJECTILE
    RENDERSTYLE ADD
    +SPAWNSOUNDSOURCE
	//+NOTIMEFREEZE
	+MTHRUSPECIES
	+SKYEXPLODE
    ALPHA 0.8
    SeeSound "plasmacaster/fly"
    DeathSound "plasmacaster/explode"
    Decal HeatScorch
    States
    {
    Spawn:
        STR2 ABCD 2 A_CustomMissile("PlasmaCasterFX",0,0,0,0)
        Loop
    Death:
	    STR2 ABCD 4 A_SpawnItemEx("BlueParticleSpawner",0,0,0,0,0,0,0,128)
		TNT1 A 0 A_Explode(8,512,0)
	    TNT1 A 0 A_Explode(38,256,0)
	    TNT1 A 0 A_Explode(128,164,0)	
        TNT1 AAA 0 A_SpawnItemEx("BlueParticleSpawner",0,0,0,0,0,0,0,128)  
	    TNT1 AAA 0 A_CustomMissile("SmokeFX3",0,0,random(0,360),2,random(0,360))
        TNT1 AAAAAA 0 A_CustomMissile("Kaboom24",0,0,random(0,360),2,random(0,360))
        STR2 EFGHIJKLMNO 3
        Stop
    }
}

ACTOR PlasmaCasterFX : ClientSideEffect
{   
   Radius 2
   Height 2
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   +CLIENTSIDEONLY
   Scale 1.2
   ALPHA 0.67
   States
   {
   Spawn:
   	  TNT1 A 0
	  TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(304) == 1, "Toaster")
	  TNT1 A 1
      ST8R PQ 4 Bright A_FadeOut(0.1)
      Loop
   }
}

ACTOR RailgunProj : FastProjectile
{
Radius 6
Height 6
Speed 300
Damage (Random(120,150))
DeathSound "cyberrail/impact"
MissileType "RailgunTrail"
+RIPPER
+MTHRUSPECIES
+EXTREMEDEATH
//+NOTIMEFREEZE
+BLOODLESSIMPACT
Decal HeatScorch
States
{
Spawn:
    TNT1 A 1
    Loop
Death:
	TNT1 A 0
	TNT1 AA 0 A_CustomMissile("SmokeFX3",0,0,random(0,360),2,random(0,360))
    TNT1 AAAA 0 A_CustomMissile("Kaboom24",0,0,random(0,360),2,random(0,360))	
	TNT1 A 0 A_Explode(32,256,0)
	TNT1 A 25 A_SpawnItem("RailgunImpact")
	Stop
	}
}

ACTOR RailgunTrail : ClientSideEffect
{
RenderStyle Add
+CLIENTSIDEONLY
+NOBLOCKMAP
+DONTSPLASH
+NOCLIP
+NOINTERACTION
+THRUACTORS
Scale 1
States
{
Spawn:
	RGPB ABCDEFGHIJKLMNOP 2 A_FadeOut(0.04)
	Stop
	}
}

ACTOR PlasmaTank_UsedWait1
{
  Radius 12
  Height 32
  States
  {
  Spawn:
    MECH CC 35
    MECH C 1 A_SpawnItemEx("PlasmaTank_Used1")
    Stop
  }
}

ACTOR PlasmaTank_UsedWait2 : PlasmaTank_UsedWait1
{
  States
  {
  Spawn:
    MECH CC 35
    MECH C 1 A_SpawnItemEx("PlasmaTank_Used2")
    Stop
  }
}

ACTOR PlasmaTank_UsedWait3 : PlasmaTank_UsedWait1
{
  States
  {
  Spawn:
    MECH CC 35
    MECH C 1 A_SpawnItemEx("PlasmaTank_Used3")
    Stop
  }
}

ACTOR PlasmaTank_Destroyed
{
  States
  {
  Spawn:
    MECH C 75
    MTPK K 4 A_PlaySound("ZTank/death")
	MTPK KKKKKKKKKKKKKKKLLLLLMMMMMNNNNN 2 Bright A_SpawnItemEx("NewSmallKaboomEffect",random(-24,24),random(-24,24),random(8,48))
    TNT1 A 0 A_Fall
	TNT1 A 0 A_SpawnItemEx("SmallVehicleExplosionEffect") 
    MTPK O -1
    Stop
  }
}