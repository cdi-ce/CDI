//=================================================\\
// --------------- Bomb Elemental ---------------- \\
//=================================================\\

ACTOR BombElementalGiver : MechGiver{Powerup.Type "BombElemental"}

ACTOR PowerBombElemental : PowerMorph
{
  PowerMorph.PlayerClass "Demon_BombElemental"
  PowerMorph.MorphFlash "TeleportFog"
  PowerMorph.UnMorphFlash "TeleportFog"
  PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE|MRF_LOSEACTUALWEAPON)
}

ACTOR Demon_BombElemental : Demon_Cydestroyer
{
  Health 400
  Player.MaxHealth 400
  Player.SoundClass "PBombElemental"
  Player.DisplayName "Bomb Elemental"
  Player.MorphWeapon "BombElemental-Weapons"
  ActiveSound "bomb/lava"
  DamageType "Marine"
  RadiusDamageFactor 0.5
  -NORADIUSDMG
  States
  {
   Spawn:   
	TNT1 A 0 A_JumpIf(user_effect == 1,3)
	TNT1 A 0 A_SpawnItemEx("PlayerDemonRuneMarkFollower",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SetUserVar("user_effect",1)
	TNT1 A 0 A_GiveInventory("IsDemon",1)
    TNT1 A 0 SetPlayerProperty(0,1,3)
	TNT1 A 0 A_LoopActiveSound
	INFP AABBCCDD 3 Bright
	Loop
   See:
   	TNT1 A 0 A_JumpIf(user_effect == 1,3)
	TNT1 A 0 A_SpawnItemEx("PlayerDemonRuneMarkFollower",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SetUserVar("user_effect",1)
	TNT1 A 0 A_GiveInventory("IsDemon",1)
    TNT1 A 0 SetPlayerProperty(0,1,3)
	TNT1 A 0 A_LoopActiveSound
	INFP AABBCCDD 3 Bright
	Goto Spawn
  Melee:
  Missile:
    INFP E 1 Bright
	INFP E 0 A_LoopActiveSound 
	INFP E 0 A_JumpIfInventory("MissileType4",1,"Missile4")
	INFP E 0 A_JumpIfInventory("MissileType3",1,"Missile3")
	INFP E 0 A_JumpIfInventory("MissileType2",1,"Missile2")
    INFP EF 5 Bright
    INFP G 4 Bright
	INFP FE 3 Bright
	Goto Spawn
  Missile2:
	INFP E 0 A_TakeInventory("MissileType2",1)
    INFP EF 5 Bright
    INFP G 35 Bright
	INFP FE 3 Bright
    Goto Spawn
  Missile3:
	INFP H 0 A_TakeInventory("MissileType3",1)
	INFP HI 6 Bright
    INFP J 5 Bright
	INFP FE 4 Bright
    Goto Spawn	
  Missile4://Rictus
	INFP H 0 A_TakeInventory("MissileType3",1)
	INFP HI 6 Bright
    INFP J 12 Bright
	INFP FE 4 Bright
    Goto Spawn		
  Pain:
	INFP K 4 A_LoopActiveSound
    INFP K 4 Bright A_Pain
    Goto Spawn
  Death:
    TNT1 A 0 A_StopSound
	TNT1 A 0 A_PlaySound("bomb/explode")
	INFP L 5 Bright A_Explode(192,156,0)
	TNT1 A 0 A_SpawnItemEx("BombGoreGenerator",0,0,0,0,0,0,0,128,0)		
	INFP M 5 Bright A_PlaySound("Pain/Death")
	INFP N 5 Bright A_NoBlocking
	TNT1 A 0 A_SetTranslucent(0.75,1)
	TNT1 AAAA 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, random(20,40), 0, 0, 0, 0, 128)
	TNT1 AAAA 0 A_CustomMissile("SmokeFX2", random(20,40), 0, random(0,360), 2, random(0,360))
	TNT1 AAAA 0 A_CustomMissile("Kaboom3", random(20,40), 0, random(0,360), 2, random(0,360))
	INFP O 5 Bright
	INFP PQ 5 Bright
	TNT1 A 1 A_HideThing
    TNT1 A -1
	Stop
  }
}

Actor BombElemental-Weapons : Weapon
{
  +WEAPON.DONTBOB
  +WEAPON.CHEATNOTWEAPON
  +WEAPON.AMMO_OPTIONAL
  +WEAPON.ALT_AMMO_OPTIONAL
  +INVENTORY.UNDROPPABLE
  States
  {
  Ready:
    TNT1 A 0 A_GiveInventory("CanHold",1)
    TNT1 A 0 A_TakeInventory("CanHalfHold",1)
    TNT1 A 0 A_TakeInventory("AttackCharge",10)
    CACL A 1 A_WeaponReady
    Loop
  Deselect: 
    CACL A 1 A_Lower
    Loop
  Select:
    CACL A 1 A_Raise
    Loop
  Fire:
    CACL A 1 A_JumpIfInventory("AttackCharge",10,"Fire2")
    CACL A 0 A_GiveInventory("AttackCharge",1)
    CACL A 1 A_Refire
  Fire1:
    CACL A 0 A_JumpIfInventory("AttackCharge",10,"Fire2")
	CACL A 0 A_TakeInventory("AttackCharge",10)
    CACL ABC 3
    CACL C 0 A_SetBlend("Orange",0.45,25)
	CACL C 0 A_FireCustomMissile("PlayerBombBall",5,0,0,5)
	CACL C 0 A_FireCustomMissile("PlayerBombBall",-5,0,0,5)
    CACL C 6 A_FireCustomMissile("PlayerBombBall",0,0,0,5)
    CACL CBA 3
    Goto Ready
  Fire2:
    CACL A 0 A_GiveInventory("MissileType2")
	CACL A 0 A_GunFlash
    CACL ABC 3
    CACL C 0 A_SetBlend("Orange",0.45,25)
	CACL C 0 A_PlayWeaponSound("magma/firebreath")
    CACL CCCCCCCCCC 3 A_FireCustomMissile("PlayerBombBreath",random(-4,4),0,0,5)
	CACL C 0 A_TakeInventory("AttackCharge",10)
    CACL CBA 3
    Goto Ready
  AltFire:
    CACL A 1 A_JumpIfInventory("AttackCharge",10,"AltFire2")
    CACL A 0 A_GiveInventory("AttackCharge",1)
    CACL A 1 A_Refire
  AltFire1:
    CACL A 0 A_JumpIfInventory("AttackCharge",10,"AltFire2")
	CACL A 0 A_TakeInventory("AttackCharge",10)
	CACL A 0 A_GiveInventory("MissileType3")
	CACL A 0 A_GunFlash	
	CACL ABC 3
    CACL C 0 A_SetBlend("Orange",0.45,25)
    CACL C 6 A_SpawnItemEx("PlayerBombRictus",45,0,16,15,0,0,0,32)
    CACL CBA 3
    Goto Ready
  AltFire2:
    CACL A 0 A_GiveInventory("MissileType4")
	CACL A 0 A_GunFlash	
	CACL ABC 3
    CACL C 0 A_SetBlend("Orange",0.45,25)
    CACL C 5 A_FireCustomMissile("PlayerBombRictusProj",-5,0,0,5)
	CACL C 5 A_FireCustomMissile("PlayerBombRictusProj",5,0,0,5)
	CACL C 5 A_FireCustomMissile("PlayerBombRictusProj",0,0,0,5)
	CACL C 0 A_TakeInventory("AttackCharge",10)
    CACL CBA 3
    Goto Ready
  Spawn:
    TNT1 A 1
    Stop
  }
}

ACTOR PlayerBombBall : BombBall
{
Damage 7
Damagetype "Marine"
-THRUSPECIES
+MTHRUSPECIES
+SCREENSEEKER
States
{
Spawn:
    TNT1 A 0 A_SpawnItemEx("BombBallTrail",0,0,0,0,0,0,0,128) 
    PBMB A 2 Bright	A_SeekerMissile(25,35,SMF_LOOK,45,2)
	PBMB B 2 Bright A_SpawnItemEx("BombBallTrail",0,0,0,0,0,0,0,128)
	Loop
    }
}

ACTOR PlayerBombBreath : BombBreath
{ 
	Damage 3
	Damagetype "Marine"
	-THRUSPECIES
	+MTHRUSPECIES
	+FORCERADIUSDMG
    States 
    { 
    Spawn: 
        FIR1 LKJIHGFEDCBA 3 Bright A_Explode(3,92) 
		FIR1 AAAA 1 Bright A_FadeOut(0.15)
	    Stop
		} 
}

Actor PlayerBombRictusProj
{		
Radius 8
Height 8
Speed 20
Damage 26
Scale 0.6
SeeSound "rictus/attack"
DeathSound "rictus/die"
Damagetype "Marine"
PROJECTILE
+RANDOMIZE
+DONTREFLECT
+MTHRUSPECIES
+SEEKERMISSILE
+SCREENSEEKER
+FORCERADIUSDMG
+EXPLODEONWATER
+SKYEXPLODE
Decal DoomImpScorch
States
{
  Spawn:
    RICT B 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
    RICT B 1 Bright A_SeekerMissile(25,35,SMF_LOOK,45,2)
    Loop
   Death:
    TNT1 A 0
	TNT1 A 0 A_Explode(52,128,0)
	TNT1 A 0 A_SetTranslucent(0.75,1)
	TNT1 AA 0 A_SpawnItemEx("SoulPart1",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
	TNT1 A 0 A_SpawnItemEx("SoulPart2",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
	TNT1 AA 0 A_SpawnItemEx("SoulPart3",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
    TNT1 AA 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
	TNT1 AAA 0 A_CustomMissile("Kaboom3", 0, 0, random(0,360), 2, random(0,360))
	TNT1 AAAA 0 A_CustomMissile("SmokeFX2", 0, 0, random(0,360), 2, random(0,360))
    MISL B 5 Bright
	MISL CD 4 Bright
    Stop
  }
}

ACTOR PlayerBombRictus : Rictus
{
DropItem ""
+FRIENDLY
+QUICKTORETALIATE
+LOOKALLAROUND
+NEVERRESPAWN
+NOBLOCKMONST
-COUNTKILL
DamageFactor "Marine", 0
DamageFactor "Legendary", 0
DamageFactor "MechStep", 0
DamageFactor "Mine", 0.0
Species "Player"
var int user_delay;
var int user_script;
States
{
Spawn:
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	TNT1 A 0 A_JumpIf(user_delay >= 1050,"Done")
	TNT1 A 0 A_SetUserVar("user_delay",user_delay+1)
	TNT1 A 0 A_JumpIf(user_script == 1,3)
	TNT1 A 0 A_SpawnItemEx("PlayerRictusFriendMarkFollower",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    TNT1 A 0 A_SetUserVar("user_script",1)
	RICT A 2 Bright A_Look
	Loop
See:
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	TNT1 A 0 A_JumpIf(user_delay >= 1050,"Done")
	TNT1 A 0 A_SetUserVar("user_delay",user_delay+1)
	TNT1 A 0 A_JumpIf(user_script == 1,3)
	TNT1 A 0 A_SpawnItemEx("PlayerRictusFriendMarkFollower",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    TNT1 A 0 A_SetUserVar("user_script",1)
	RICT A 2 Bright A_Chase
	Loop
Missile:
	TNT1 A 0 A_Jump(64,"Kamikaze")
Normal:
	TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT A 2 Bright A_FaceTarget
	TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT A 2 Bright A_FaceTarget
	TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT B 2 Bright A_CustomMissile("PlayerBombRictusBall",0,0,0)
    RICT BA 2 Bright A_SpawnItem("RictFireSpawner",0,0,0,0)
    Goto See
Kamikaze:
	TNT1 A 0 A_JumpIfCloser(450,1)
	Goto Normal
	TNT1 A 0
	Goto PainSuicide+3
Done:
    RICT A 0 A_JumpIfTargetInLos(2)
	RICT A 2 Bright A_Die
    Goto See
	TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT B 3 Bright A_Pain
	TNT1 A 0 A_Jump(28,"See")
	TNT1 A 0 A_ChangeFlag(NOTARGET,1)
	RICT B 1 Bright A_ChangeFlag(NOPAIN,1)
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
    TNT1 A 0 A_CustomMissile("PlayerBombRictusSuicide",0,0,0,0)
	RICT B 1 Bright A_Die("Suicide")
    Stop
Pain:
	TNT1 A 0 A_JumpIfHealthLower(35,"PainSuicide")
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT B 3 Bright A_Pain
	Goto See
PainSuicide:
    TNT1 A 0 A_JumpIfCloser(450,1)
	Goto See
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
	RICT B 3 Bright A_Pain
	TNT1 A 0 A_Jump(28,"See")
	TNT1 A 0 A_ChangeFlag(NOTARGET,1)
	RICT B 1 Bright A_ChangeFlag(NOPAIN,1)
    TNT1 A 0 A_SpawnItem("RictFireSpawner",0,0,0,0)
    TNT1 A 0 A_CustomMissile("PlayerBombRictusSuicide",0,0,0,0)
	RICT B 1 Bright A_Die("Suicide")
    Stop
Death:
    TNT1 A 0
	TNT1 A 0 A_Scream
	TNT1 A 0 A_SetTranslucent(0.75,1)
	TNT1 A 0 A_GiveInventory("ImDead",1)
	TNT1 AA 0 A_SpawnItemEx("SoulPart1",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
	TNT1 A 0 A_SpawnItemEx("SoulPart2",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
	TNT1 AA 0 A_SpawnItemEx("SoulPart3",0,0,Random(10,20),Random(8,-8),Random(8,-8),Random(6,8))
    TNT1 AA 0 A_SpawnItemEx("OrangeParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128) 
	TNT1 AAA 0 A_CustomMissile("Kaboom3", 0, 0, random(0,360), 2, random(0,360))
	TNT1 AAAA 0 A_CustomMissile("SmokeFX2", 0, 0, random(0,360), 2, random(0,360))
    MISL B 5 Bright A_NoBlocking
	MISL CD 4 Bright A_GiveInventory("ImDead",1)
    Stop
Death.Suicide:
	TNT1 A 0
	TNT1 A 0 A_GiveInventory("ImDead",1)
    TNT1 A 100 A_NoBlocking
    Stop
    }
}

ACTOR PlayerRictusFriendMarkFollower
{
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+NOINTERACTION
+NOGRAVITY
var int user_delay;
States
{
  Spawn:
    TNT1 A 1
	TNT1 A 1 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
	TNT1 A 0 A_SpawnItemEx("MorphedPlayerFriendMark",0,0,38,0,0,0,0,32)
	TNT1 A 0 A_JumpIfInventory("ImDead",1,"Death",AAPTR_TARGET)
	TNT1 A 0 A_JumpIf(user_delay >= 2200,"Death")
	TNT1 A 0 A_SetUserVar("user_delay",user_delay+1)
	Loop
  Death:
	TNT1 A 1
	Stop
	}
}

ACTOR PlayerBombRictusBall : RictusBall
{
Damagetype "Marine"
-THRUSPECIES
+MTHRUSPECIES
}

ACTOR PlayerBombRictusSuicide : RictusSuicide
{
Damagetype "Marine"
-THRUSPECIES
+MTHRUSPECIES
}