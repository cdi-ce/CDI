//=================================================\\
// ------------------- Enforcer ------------------ \\
//=================================================\\

Actor Enforcer : MechPickup
{
  States
  {
  Spawn:
    AGRD A 1 A_SpawnItemEx("SuitIndicator",0,0,64)
    Loop
  Pickup:
  	NULL A 1
	NULL A 1 A_JumpIf(ACS_ExecuteWithResult(3703,0,0,0) == 1 ,"Nope")
	NULL A 1 ACS_ExecuteAlways(2994,0,0,0,0)
    NULL A 1 A_GiveInventory("EnforcerGiver")
	Stop
  Nope:
	AGRD A 1
	Stop
  }
}

ACTOR EnforcerGiver : MechGiver{Powerup.Type "Enforcer"}

ACTOR PowerEnforcer : PowerMorph
{
  PowerMorph.PlayerClass "Suit_Enforcer"
  PowerMorph.MorphFlash "Nothing"
  PowerMorph.UnMorphFlash "Nothing"
  PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_NEWTIDBEHAVIOUR|MRF_WHENINVULNERABLE|MRF_LOSEACTUALWEAPON)
}

ACTOR Suit_Enforcer : Suit_MaxSoldier
{
  Speed 0.6
  Mass 400
  Health 400
  Player.MaxHealth 400
  Player.ForwardMove 1.0
  Player.SideMove 1.0
  Player.ViewHeight 50
  Player.AttackZOffset 16
  Player.JumpZ 8
  Player.DisplayName "Enforcer"
  Player.MorphWeapon "Enforcer-Weapons"
  PainChance 1
  DamageFactor 0.4
  RadiusDamageFactor 0.8
  States
  {
   Spawn:
    AGRD A -1
    Loop
   See:
    AGRD AABB 3
    TNT1 A 0 A_PlaySound("b1battlebot/step")
    AGRD CCDD 3
    TNT1 A 0 A_PlaySound("b1battlebot/step")
    Goto Spawn
   Missile:
    AGRD E 12 
    Goto Spawn
   Melee:
    AGRD F 6 Bright
    Goto Missile
   Pain:
    AGRD O 4 
    AGRD O 4 A_Pain
    Goto Spawn
   Death:
    AGRD G 3 A_SpawnItemEx("Kaboom",Random(-24,24),Random(-24,24),Random(4,48))
	TNT1 A 0 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	AGRD H 3 A_SpawnItemEx("Kaboom",Random(-24,24),Random(-24,24),Random(4,48))
	TNT1 A 0 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	AGRD I 3 A_PlayerScream
	TNT1 A 0 A_SpawnItemEx("GibbedGenerator",0,0,0,0,0,0,0,128,0)
	TNT1 A 0 A_SpawnItemEx("Kaboom",Random(-24,24),Random(-24,24),Random(4,48))
	AGRD J 3 A_NoBlocking
	AGRD K 3
	AGRD L 3 A_SpawnItem("BodyCrash",0,0,0,0)
	AGRD M 3
	AGRD N -1
	Stop
   ShieldMoving:
    AGRD E 24
	TNT1 A 0 A_Playsound("monster/bruwlk")
	goto Spawn
  }
}

Actor Enforcer-Weapons : Weapon
{
+INVENTORY.UNDROPPABLE
+WEAPON.AMMO_OPTIONAL
+WEAPON.ALT_AMMO_OPTIONAL
+WEAPON.CHEATNOTWEAPON
States
{
Ready:
EWPA A 1 A_WeaponReady
loop
Select:
EWPA A 1 A_Raise
loop
Deselect:
EWPA A 1 A_Lower
loop
Fire:
TNT1 A 0 A_PlaySound("weapons/enforcer/fire",7)
TNT1 A 0 A_PlaySound("weapons/enforcer/add",3)
TNT1 AAAAAAAAAAAAA 0 Bright A_FireCustomMissile("SGTracer",Random(8,-8),0,10,Random(8,-8))
TNT1 A 0 A_FireBullets(13.5,7.4,26,5,"ModdedBulletPuff")
TNT1 A 0 A_Light1
TNT1 A 0 Radius_Quake(6,3,0,1,0)
EWPF A 1 Bright
EWPF B 1 Bright
TNT1 A 0 A_Light0
EWPF C 1
EWPF D 1
EWPF E 1
EWPF F 1
EWPF G 1
EWPF F 1
EWPF E 1
EWPF D 1
EWPF H 1
EWPF I 1
EWPA A 2
EWPR ABC 1
TNT1 A 0 A_PlaySound("weapons/enforcer/spin",7,1.5)
EWPR DEFG 1
TNT1 A 0 A_PlaySound("weapons/enforcer/reload",3)
EWPR HIJ 1
TNT1 AA 0 A_SpawnItemEx("ShellCasing",2,4,33,Random(2,4),Random(3,6),Random(3,6),0)
EWPR KLMNOPQRSTUVWXY 1
EWPA A 5
goto ready
AltFire:
TNT1 A 0 A_PlaySound("weapons/enforcer/shield",3)
EWPB ABCDEFGH 1
TNT1 A 0 ACS_ExecuteAlways(3104,0,11)
AltHold:
EWPB H 1
TNT1 A 0 A_GiveInventory("EnforcerProtectionGiver",1)
TNT1 A 0 A_ReFire
TNT1 A 0 ACS_ExecuteAlways(3104,0,12)
EWPB GFEDCBA 1
goto Ready
}
}

Actor PowerEnforcerProtection : PowerProtection 
{
DamageFActor "Normal", 0.2
+NORADIUSDMG
Inventory.Icon "MEGAA0"
}

Actor EnforcerProtectionGiver : PowerupGiver
{
  Inventory.MaxAmount 0
  Powerup.Type EnforcerProtection
  Powerup.Color White 0.0
  Powerup.Duration -1
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  States
  {
  Spawn:
    NULL A 1 Bright
    Loop
  }
}

ACTOR Enforcer_Destroyed
{
  States
  {
  Spawn:
    AGRD A 70
    AGRD GH 3 A_SpawnItemEx("Kaboom",Random(-24,24),Random(-24,24),Random(4,48))
	AGRD I 3
	TNT1 A 0 A_SpawnItemEx("Kaboom",Random(-24,24),Random(-24,24),Random(4,48))
	AGRD J 3 A_NoBlocking
	AGRD K 3
	AGRD L 3 A_SpawnItem("BodyCrash",0,0,0,0)
	AGRD M 3
	AGRD N -1
	Stop
  }
}